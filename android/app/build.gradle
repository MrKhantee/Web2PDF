apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"


    def getVersionCode = { ->
        try {
            def stdout = new ByteArrayOutputStream()
                exec {
                    commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
                        standardOutput = stdout
                }
            return Integer.parseInt(stdout.toString().trim())
        }
        catch (ignored) {
            return -1;
        }
    }

    def getVersionName = { ->
        try {
            def stdout = new ByteArrayOutputStream()
                exec {
                    commandLine 'git', 'describe', '--tags', '--dirty'
                        standardOutput = stdout
                }
            return stdout.toString().trim()
        }
        catch (ignored) {
            return null;
        }
    }


    defaultConfig {
        applicationId "org.dyndns.warenix.web2pdf"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode getVersionCode()
        versionName getVersionName()
    }
    buildTypes {
        release {
            minifyEnabled true
	    shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

ext.supportVersion = '27.0.1'
ext.permissionDispatcherVersion = '2.3.1'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:support-core-utils:${supportVersion}"
    compile "com.android.support:support-compat:${supportVersion}"
    compile "com.android.support:recyclerview-v7:${supportVersion}"
    compile "com.android.support:support-fragment:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile "com.github.hotchemi:permissionsdispatcher:${permissionDispatcherVersion}"
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:${permissionDispatcherVersion}"
}
